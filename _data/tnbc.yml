- name: Genes list linked through pathways on triple-negative breast cancer
  rq: |
    #title: Genes list linked through pathways on triple-negative breast cancer
    PREFIX wd:<http://www.wikidata.org/entity/>
    PREFIX wdt: <http://www.wikidata.org/prop/direct/>
    PREFIX wikibase:<http://wikiba.se/ontology#>
    PREFIX bd:<http://www.bigdata.com/rdf#>

    SELECT DISTINCT ?gene ?geneLabel ?scholia WHERE {
        VALUES ?disease {wd:Q7843332} # with a main subject of triple negative breast cancer
        ?pathway wdt:P2410 ?wpid ;      # pathways with a Wikipathways ID
                wdt:P921 ?disease ;
                wdt:P527 ?gene .      # which has various parts
        ?gene wdt:P31 wd:Q7187 .       # part is of part gene
        BIND(uri(CONCAT("https://scholia.toolforge.org/gene/",REPLACE(str(?gene), "http://www.wikidata.org/entity/", ""))) as ?scholia)

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
    }

- name: Concatenated list of TNBC genes
  height: 15
  md: |
    This simplified list can be copied under <a href="data.html#network">STRING Network Interactions</a>
    to explore network.
  rq: |
    #title: Genes list linked through pathways on triple-negative breast cancer
    SELECT (GROUP_CONCAT(?geneLabel; SEPARATOR=" ") as ?geneList) WHERE {
      VALUES ?disease {
        wd:Q7843332
      }
      ?pathway wdt:P2410 ?wpid;
        wdt:P921 ?disease;
        wdt:P527 ?gene.
      ?gene wdt:P31 wd:Q7187.
      ?gene rdfs:label ?geneLabel.
      FILTER(LANG(?geneLabel) = "en")
    }
  post: |
    ## STRING Network Interactions {#network}

    The list of genes from Wikidata can be entered here to visualize
    the network interactions from https://string-db.org. The list
    shown by default is from TNBC:

    <center>
        <input type="text" id='inputarea' placeholder='PKM PDK1 PSPH PFKP SLC7A11 LS SLC2A1 HK1 LDHA PDP1 PSAT1 SLC16A1 PHGDH SLC1A5'/>
        <br/>
        <button onclick='javascript:send_request_to_string();' type="button">Let's go!</button>
        <div id="stringEmbedded"></div>
    </center>

    <script>
    $(document).ready( function () {
      send_request_to_string()
    } );
    </script>

- name: Chemical compounds part of a pathway on triple-negative breast cancer
  rq: |
    #title: Chemical compounds part of a pathway on triple-negative breast cancer

    PREFIX wd: <http://www.wikidata.org/entity/>
    PREFIX wdt: <http://www.wikidata.org/prop/direct/>
    PREFIX wikibase: <http://wikiba.se/ontology#>
    PREFIX bd: <http://www.bigdata.com/rdf#>

    SELECT DISTINCT ?compound ?compoundLabel ?scholia WHERE {
        ?pathway wdt:P2410 ?wpid ;      # pathways with a Wikipathways ID
                wdt:P921 wd:Q7843332 ; # with a main subject of triple negative breast cancer
                wdt:P527 ?compound .      # which has various parts
        ?compound wdt:P279*/wdt:P31 wd:Q11173 .       # part is subclass of of chemical compound
        BIND(uri(CONCAT("https://scholia.toolforge.org/chemical/",REPLACE(str(?gene), "http://www.wikidata.org/entity/", ""))) as ?scholia)

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
    }

- name: Cell types related to breast cancer via markers
  rq: |
    #title: Cell types related to breast cancer via markers

    PREFIX wd: <http://www.wikidata.org/entity/>
    PREFIX wdt: <http://www.wikidata.org/prop/direct/>

    SELECT ?cellTypeLabel ?scholia_cellType ?geneLabel ?scholia_gene ?diseaseLabel ?scholia_disease
    WHERE {
        VALUES ?disease { wd:Q128581} # breast cancer
        ?disease wdt:P2293 ?diseaseGene ;  # Breast cancer --> genetic association --> gene
            rdfs:label ?diseaseLabel .
        ?cellType wdt:P8872 ?diseaseGene ; # Cell type --> has marker --> gene
            rdfs:label ?cellTypeLabel .

        ?diseaseGene rdfs:label ?geneLabel .
        BIND(uri(CONCAT("https://scholia.toolforge.org/disease/",REPLACE(str(?disease), "http://www.wikidata.org/entity/", ""))) as ?scholia_disease)
        BIND(uri(CONCAT("https://scholia.toolforge.org/topic/",REPLACE(str(?cellType), "http://www.wikidata.org/entity/", ""))) as ?scholia_cellType)
        BIND(uri(CONCAT("https://scholia.toolforge.org/gene/",REPLACE(str(?diseaseGene), "http://www.wikidata.org/entity/", ""))) as ?scholia_gene)

        FILTER (LANG(?cellTypeLabel) = "en")
        FILTER (LANG(?diseaseLabel) = "en")
        FILTER (LANG(?geneLabel) = "en")
    }
